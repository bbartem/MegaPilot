// Данный пример выводит строку пошагово её сдвигая.             // 
                                                                 //
    char ticker[] = "Hello World";                               // Определяем текст для бегущей строки.
    int  i=0;                                                    // Определяем счётчик количества совершённых сдвигов.
    int  j;                                                      // Объявляем переменную для хранения количества пикселей в бегущей строке.
                                                                 //
#include <Wire.h>                                                // Подключаем библиотеку для работы с аппаратной шиной I2C.
#include <iarduino_I2C_Matrix_8x8.h>                             // Подключаем библиотеку для работы с LED матрицей 8x8.
iarduino_I2C_Matrix_8x8 disp(0x09);                              // Объявляем объект disp для работы с LED матрицей 8x8, указывая её адрес на шине I2C.
                                                                 //
void setup(){                                                    //
    delay(500);                                                  // Ждём завершение переходных процессов связанных с подачей питания.
    disp.begin();                                                // Инициируем работу с LED матрицей 8x8.
    disp.print(ticker);                                          // Загружаем текст бегущей строки. Текст только загрузится в модуль, но не начнёт выводиться!
    disp.scrollPos(0);                                           // Выводим бегущую строку на дисплей в позиции 0 (начало бегущей строки).
    delay(500);                                                  // Ждём 500 мс (задержка на первом символе строки).
    j = disp.getCharWidth()+disp.getCharInterval();              // Определяем ширину одного символа и межсимвольного интеравала бегущей строки (в пикселях).
    j*= (strlen(ticker)-1);                                      // Определяем ширину бегущей строки (в пикселях), умножив рассчитанное ранее значение на количество символов в строке.
}                                                                //
                                                                 //
void loop(){                                                     //
    disp.scrollStep();                                           // Сдвигаем строку на один шаг. (для автосдвига воспользуйтесь функцией autoScroll, а для сдвига в указанную позицию - функцией scrollPos).
    i++;                                                         // Увеличиваем счётчик совершённых сдвигов.
    if( i >= j ){                                                // Если строка прокручена полностью (количество сдвигов равно или превысило количество пикселей в строке), то ...
        i = 0;                                                   // Сбрасываем счётчик совершённых сдвигов.
        delay(1000);                                             // Ждём 1 сек (задержка на последнем символе строки).
        disp.clrScr();                                           // Чистим дисплей.
        delay(1000);                                             // Ждём 1 сек (пауза между прокрутками бегущих строк).
        disp.scrollPos(0);                                       // Выводим бегущую строку на дисплей в позиции 0 (начало бегущей строки).
        delay(1000);                                             // Ждём 1 сек (задержка на первом символе строки).
    }                                                            //
    delay(100);                                                  // Приостанавливаем выполнение кода на 0.1 сек (чем больше задержка, тем медленнее скорость бегущей строки).
}                                                                //